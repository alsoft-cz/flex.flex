----------------------------------------------------------------------------------------------------
module private types =
-- Tagged types.
-- Flex Run-time Library
-- Copyright (C) 1999-2003 A && L soft
----------------------------------------------------------------------------------------------------
--
-- Type descriptor layout:
--
--     +---------------+                                                          
--     | :size         | +0         \                                             
--     +---------------+            |                                             
--     | data type     | +4         |                                             
--     +---------------+            |                                             
--     | ancestor :tag | +8         |                                             
--     +---------------+            |                                             
--     | ^@init     *) | +12        |
--     +---------------+            |                                             
--     | ^entry        | +16        | all data types
--     +---------------+            |
--     | ^exit         | +20        |                                             
--     +---------------+            |                                             
--     | ^adjust       | +24        |                                             
--     +---------------+            |                                             
--     | ^rollback     | +28        |                                             
--     +---------------+            |                                             
--     | ^commit       | +32        /
--     +---------------+
--     | ^virtual_1    | +36        \
--     |    ...        |            | classes only
--     | ^virtual_n    | +36 + 4*n  /
--     +---------------+
--
--     *) always NIL for classes
--
----------------------------------------------------------------------------------------------------

const
  generated_procs = #environment compiler_version;='4.0.3.3';

type
  -- type descriptor
  t_type_descriptor = {%%TODO with t_entity_descriptor} record
      size         : t_rtl_unsigned32;
      data_type    : t_rtl_unsigned32;
      ancestor     : t_rtl_tag;
      spec_init    : t_rtl_pointer;
      spec_entry   : t_rtl_pointer;
      spec_exit    : t_rtl_pointer;
      spec_adjust  : t_rtl_pointer;
      spec_rollback: t_rtl_pointer;
      spec_commit  : t_rtl_pointer;
      end record;
  p_type_descriptor = ^t_type_descriptor;

#if generated_procs then;
----------------------------------------------------------------------------------------------------
#template generate_rtl_call_special_method (
    rtl_name,                                    -- desired name of the [rtl_call_*] method
    ptr_offset);                                 -- offset of the pointer to the method within the
                                                 -- type descritor
-- Generates a [rtl_call_*] method used to call the appropriate special method.
----------------------------------------------------------------------------------------------------

  --------------------------------------------------------------------------------------------------
  procedure rtl_name (
      instance       : in t_rtl_pointer);
  #pragma stackframe(rtl_name,stack_pointer_relative);
  procedure rtl_name =
  -- Calls the <special> method.
  --------------------------------------------------------------------------------------------------
  begin
    #syntax asm;
      -- class address
      mov    eax,[esp+4]
      -- load :TAG
      mov    eax,[eax]
      -- load ^<special>
  @2: mov    edx,[eax+ptr_offset]
      cmp    edx,0
      je     @1
      -- call <special>
      jmp    edx
  @1: -- load parent :TAG
      mov    eax,[eax+t_type_descriptor.ancestor]
      -- don't care about non-tagged types
      cmp    eax,0
      jne    @2
      #end asm;
    end rtl_name;

  #end generate_rtl_call_special_method;



----------------------------------------------------------------------------------------------------
#expand generate_rtl_call_special_method;
  #for rtl_name   use; rtl_call_init
  #for ptr_offset use; t_type_descriptor.spec_init
  #end generate_rtl_call_special_method;
-- Calls the INIT method.
----------------------------------------------------------------------------------------------------
#expand generate_rtl_call_special_method;
  #for rtl_name   use; rtl_call_entry
  #for ptr_offset use; t_type_descriptor.spec_entry
  #end generate_rtl_call_special_method;
-- Calls the ENTRY method.
----------------------------------------------------------------------------------------------------
#expand generate_rtl_call_special_method;
  #for rtl_name   use; rtl_call_exit
  #for ptr_offset use; t_type_descriptor.spec_exit
  #end generate_rtl_call_special_method;
-- Calls the EXIT method.
----------------------------------------------------------------------------------------------------
#expand generate_rtl_call_special_method;
  #for rtl_name   use; rtl_call_adjust
  #for ptr_offset use; t_type_descriptor.spec_adjust
  #end generate_rtl_call_special_method;
-- Calls the ADJUST method.
----------------------------------------------------------------------------------------------------
#expand generate_rtl_call_special_method;
  #for rtl_name   use; rtl_call_commit
  #for ptr_offset use; t_type_descriptor.spec_commit
  #end generate_rtl_call_special_method;
-- Calls the COMMIT method.
----------------------------------------------------------------------------------------------------
#expand generate_rtl_call_special_method;
  #for rtl_name   use; rtl_call_rollback
  #for ptr_offset use; t_type_descriptor.spec_rollback
  #end generate_rtl_call_special_method;
-- Calls the ROLLBACK method.
----------------------------------------------------------------------------------------------------

#else;
----------------------------------------------------------------------------------------------------
procedure rtl_call_init (
    instance       : in t_rtl_pointer);
#pragma stackframe(rtl_call_init,stack_pointer_relative);
procedure rtl_call_init =
-- Calls the INIT method.
----------------------------------------------------------------------------------------------------
begin
  #syntax asm;
    -- class address
    mov    eax,[esp+4]
    -- load :TAG
    mov    eax,[eax]
    -- load ^INIT
@2: mov    edx,[eax+12]
    cmp    edx,0
    je     @1
    -- call INIT
    jmp    edx
@1: -- load parent :TAG
    mov    eax,[eax+8]
    -- don't care about non-tagged types
    cmp    eax,0
    je     @3
    jmp    @2
@3:
    #end asm;
  end rtl_call_init;



----------------------------------------------------------------------------------------------------
procedure rtl_call_entry (
    instance       : in t_rtl_pointer);
#pragma stackframe(rtl_call_entry,stack_pointer_relative);
procedure rtl_call_entry =
-- Calls the ENTRY method.
----------------------------------------------------------------------------------------------------
begin
  #syntax asm;
    -- class address
    mov    eax,[esp+4]
    -- load :TAG
    mov    eax,[eax]
    -- load ^ENTRY
@2: mov    edx,[eax+16]
    cmp    edx,0
    je     @1
    -- call ENTRY
    jmp    edx
@1: -- load parent :TAG
    mov    eax,[eax+8]
    -- don't care about non-tagged types
    cmp    eax,0
    je     @3
    jmp    @2
@3:
    #end asm;
  end rtl_call_entry;



----------------------------------------------------------------------------------------------------
procedure rtl_call_exit (
    instance       : in t_rtl_pointer);
#pragma stackframe(rtl_call_exit,stack_pointer_relative);
procedure rtl_call_exit =
-- Calls the EXIT method.
----------------------------------------------------------------------------------------------------
begin
  #syntax asm;
    -- class address
    mov    eax,[esp+4]
    -- load :TAG
    mov    eax,[eax]
    -- load ^EXIT
@2: mov    edx,[eax+20]
    cmp    edx,0
    je     @1
    -- call EXIT
    jmp    edx
@1: -- load parent :TAG
    mov    eax,[eax+8]
    -- don't care about non-tagged types
    cmp    eax,0
    je     @3
    jmp    @2
@3:
    #end asm;
  end rtl_call_exit;



----------------------------------------------------------------------------------------------------
procedure rtl_call_adjust (
    instance       : in t_rtl_pointer);
#pragma stackframe(rtl_call_adjust,stack_pointer_relative);
procedure rtl_call_adjust =
-- Calls the adjust method.
----------------------------------------------------------------------------------------------------
begin
  #syntax asm;
    -- class address
    mov    eax,[esp+4]
    -- load :TAG
    mov    eax,[eax]
    -- load ^ADJUST
@2: mov    edx,[eax+24]
    cmp    edx,0
    je     @1
    -- call ADJUST
    jmp    edx
@1: -- load parent :TAG
    mov    eax,[eax+8]
    -- don't care about non-tagged types
    cmp    eax,0
    je     @3
    jmp    @2
@3:
    #end asm;
  end rtl_call_adjust;
#end if;


#if generated_procs then;
----------------------------------------------------------------------------------------------------
#template generate_rtl_ancestor_special_method (
    rtl_name,                                    -- desired name of the [rtl_ancestor_*] method
    ptr_offset);                                 -- offset of the pointer to the method within the
                                                 -- type descritor
-- Generates a [rtl_ancestor_*] method used to call the appropriate inherited special method.
----------------------------------------------------------------------------------------------------

  --------------------------------------------------------------------------------------------------
  procedure rtl_name (
      instance       : in t_rtl_pointer);
  #pragma stackframe(rtl_name,stack_pointer_relative);
  procedure rtl_name =
  -- Calls the <special> method.
  --------------------------------------------------------------------------------------------------
  begin
    #syntax asm;
      -- class address
      mov    eax,[esp+4]
      -- load ancestor :TAG
  @1: mov    eax,[eax+t_type_descriptor.ancestor]
      -- don't care about non-tagged types
      cmp    eax,0
      je     @3
      -- load ^<special>
  @2: mov    edx,[eax+ptr_offset]
      cmp    edx,0
      je     @1
      -- call <special>
      jmp    edx
  @3:
      #end asm;
    end rtl_name;

  #end generate_rtl_ancestor_special_method;



----------------------------------------------------------------------------------------------------
#expand generate_rtl_ancestor_special_method;
  #for rtl_name   use; rtl_ancestor_init
  #for ptr_offset use; t_type_descriptor.spec_init
  #end generate_rtl_ancestor_special_method;
-- Calls the INIT method.
----------------------------------------------------------------------------------------------------
#expand generate_rtl_ancestor_special_method;
  #for rtl_name   use; rtl_ancestor_entry
  #for ptr_offset use; t_type_descriptor.spec_entry
  #end generate_rtl_ancestor_special_method;
-- Calls the ENTRY method.
----------------------------------------------------------------------------------------------------
#expand generate_rtl_ancestor_special_method;
  #for rtl_name   use; rtl_ancestor_exit
  #for ptr_offset use; t_type_descriptor.spec_exit
  #end generate_rtl_ancestor_special_method;
-- Calls the EXIT method.
----------------------------------------------------------------------------------------------------
#expand generate_rtl_ancestor_special_method;
  #for rtl_name   use; rtl_ancestor_adjust
  #for ptr_offset use; t_type_descriptor.spec_adjust
  #end generate_rtl_ancestor_special_method;
-- Calls the ADJUST method.
----------------------------------------------------------------------------------------------------
#expand generate_rtl_ancestor_special_method;
  #for rtl_name   use; rtl_ancestor_commit
  #for ptr_offset use; t_type_descriptor.spec_commit
  #end generate_rtl_ancestor_special_method;
-- Calls the COMMIT method.
----------------------------------------------------------------------------------------------------
#expand generate_rtl_ancestor_special_method;
  #for rtl_name   use; rtl_ancestor_rollback
  #for ptr_offset use; t_type_descriptor.spec_rollback
  #end generate_rtl_ancestor_special_method;
-- Calls the ROLLBACK method.
----------------------------------------------------------------------------------------------------


#else;
----------------------------------------------------------------------------------------------------
procedure rtl_ancestor_init (
    instance       : in t_rtl_pointer);
#pragma stackframe(rtl_ancestor_init,stack_pointer_relative);
procedure rtl_ancestor_init =
-- Calls the INIT method.
----------------------------------------------------------------------------------------------------
begin
  #syntax asm;
    -- class address
    mov    eax,[esp+4]
    -- load ancestor :TAG
@1: mov    eax,[eax+8]
    -- don't care about non-tagged types
    cmp    eax,0
    je     @3
    -- load ^INIT
@2: mov    edx,[eax+12]
    cmp    edx,0
    je     @1
    -- call INIT
    jmp    edx
@3:
    #end asm;
  end rtl_ancestor_init;



----------------------------------------------------------------------------------------------------
procedure rtl_ancestor_entry (
    instance       : in t_rtl_pointer);
#pragma stackframe(rtl_ancestor_entry,stack_pointer_relative);
procedure rtl_ancestor_entry =
-- Calls the ENTRY method.
----------------------------------------------------------------------------------------------------
begin
  #syntax asm;
    -- class address
    mov    eax,[esp+4]
    -- load ancestor :TAG
@1: mov    eax,[eax+8]
    -- don't care about non-tagged types
    cmp    eax,0
    je     @3
    -- load ^ENTRY
@2: mov    edx,[eax+16]
    cmp    edx,0
    je     @1
    -- call ENTRY
    jmp    edx
@3:
    #end asm;
  end rtl_ancestor_entry;



----------------------------------------------------------------------------------------------------
procedure rtl_ancestor_exit (
    instance       : in t_rtl_pointer);
#pragma stackframe(rtl_ancestor_exit,stack_pointer_relative);
procedure rtl_ancestor_exit =
-- Calls the EXIT method.
----------------------------------------------------------------------------------------------------
begin
  #syntax asm;
    -- class address
    mov    eax,[esp+4]
    -- load ancestor :TAG
@1: mov    eax,[eax+8]
    -- don't care about non-tagged types
    cmp    eax,0
    je     @3
    -- load ^EXIT
@2: mov    edx,[eax+20]
    cmp    edx,0
    je     @1
    -- call EXIT
    jmp    edx
@3:
    #end asm;
  end rtl_ancestor_exit;



----------------------------------------------------------------------------------------------------
procedure rtl_ancestor_adjust (
    instance       : in t_rtl_pointer);
#pragma stackframe(rtl_ancestor_adjust,stack_pointer_relative);
procedure rtl_ancestor_adjust =
-- Calls the ADJUST method.
----------------------------------------------------------------------------------------------------
begin
  #syntax asm;
    -- class address
    mov    eax,[esp+4]
    -- load ancestor :TAG
@1: mov    eax,[eax+8]
    -- don't care about non-tagged types
    cmp    eax,0
    je     @3
    -- load ^ADJUST
@2: mov    edx,[eax+24]
    cmp    edx,0
    je     @1
    -- call ADJUST
    jmp    edx
@3:
    #end asm;
  end rtl_ancestor_adjust;
#end if;


#if #declared ondra; then;
----------------------------------------------------------------------------------------------------
procedure rtl_is_ancestor (
    A              : in t_rtl_tag;
    _B             : in t_rtl_tag)
    return t_rtl_logical32 =
-- Checks whether A is an ancestor of B (A < B).
----------------------------------------------------------------------------------------------------
var
  B                : t_rtl_tag;

begin
  B:=_B;
  while B<>nil loop
    B:=p_type_descriptor(B:unchecked)^.ancestor;
    if A=B then 
      break;
      end if;
    end loop;
  result:=B<>nil;
  end rtl_is_ancestor;



----------------------------------------------------------------------------------------------------
procedure rtl_is_ancestor_or_equal (
    A              : in t_rtl_tag;
    B              : in t_rtl_tag)
    return t_rtl_logical32 =
-- Checks whether A is an ancestor of B or they are equal (A <= B).
----------------------------------------------------------------------------------------------------
var
  B                : t_rtl_tag;

begin
  B:=_B;
  while B<>nil and then A<>B loop
    B:=p_type_descriptor(B:unchecked)^.ancestor;
    end loop;
  result:=B<>nil;
  end rtl_is_ancestor_or_equal;



----------------------------------------------------------------------------------------------------
procedure rtl_is_descendant (
    _A             : in t_rtl_tag;
    B              : in t_rtl_tag)
    return t_rtl_logical32 =
-- Checks whether A is a descendant of B (A > B).
----------------------------------------------------------------------------------------------------
var
  _A               : t_rtl_tag;

begin
  A:=_A;
  while A<>nil loop
    A:=p_type_descriptor(A:unchecked)^.ancestor;
    if A=B then 
      break;
      end if;
    end loop;
  result:=A<>nil;
  end rtl_is_descendant;



----------------------------------------------------------------------------------------------------
procedure rtl_is_descendant_or_equal (
    _A             : in t_rtl_tag;
    B              : in t_rtl_tag)
    return t_rtl_logical32 =
-- Checks whether A is a descendant of B or they are equal (A >= B).
----------------------------------------------------------------------------------------------------
var
  A                : t_rtl_tag;

begin
  A:=_A;
  while A<>nil and then A<>B loop
    A:=p_type_descriptor(A:unchecked)^.ancestor;
    end loop;
  result:=A<>nil;
  end rtl_is_descendant_or_equal;
#end if;



---- Compiler magic --------------------------------------------------------------------------------
#pragma assign(call_init             ,flex.tags.types.rtl_call_init             );
#pragma assign(call_entry            ,flex.tags.types.rtl_call_entry            );
#pragma assign(call_exit             ,flex.tags.types.rtl_call_exit             );
#pragma assign(call_adjust           ,flex.tags.types.rtl_call_adjust           );
#if #declared rtl_call_rollback; then;
#pragma assign(call_rollback         ,flex.tags.types.rtl_call_rollback         );
#pragma assign(call_commit           ,flex.tags.types.rtl_call_commit           );
#end if;
#pragma assign(ancestor_init         ,flex.tags.types.rtl_ancestor_init         );
#pragma assign(ancestor_entry        ,flex.tags.types.rtl_ancestor_entry        );
#pragma assign(ancestor_exit         ,flex.tags.types.rtl_ancestor_exit         );
#pragma assign(ancestor_adjust       ,flex.tags.types.rtl_ancestor_adjust       );
#if #declared rtl_call_rollback; then;
#pragma assign(ancestor_rollback     ,flex.tags.types.rtl_ancestor_rollback     );
#pragma assign(ancestor_commit       ,flex.tags.types.rtl_ancestor_commit       );
#end if;
#if #declared ondra; then;
#pragma assign(is_ancestor           ,flex.tags.types.rtl_is_ancestor           );
#pragma assign(is_ancestor_or_equal  ,flex.tags.types.rtl_is_ancestor_or_equal  );
#pragma assign(is_descendant         ,flex.tags.types.rtl_is_descendant         );
#pragma assign(is_descendant_or_equal,flex.tags.types.rtl_is_descendant_or_equal);
#end if;
----------------------------------------------------------------------------------------------------



end types;